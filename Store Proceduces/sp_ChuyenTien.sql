create PROC [dbo].[sp_ChuyenTien] (@STKGUI nChar(9), @STKNHAN nChar(9), @TIEN MONEY, @NGAYGD datetime, @MANV NCHAR(10))
AS
SET XACT_ABORT ON
BEGIN
	--KIEM TRA STK_GUI VA STK_NHAN CO TON TAI TRONG DB HAY KHONG, SAU DO THI KIEM TRA SO DU NGUOI GUI.
	IF ( EXISTS( SELECT SOTK FROM dbo.TAIKHOAN WHERE SOTK=@STKNHAN ) AND EXISTS( SELECT SOTK FROM dbo.TAIKHOAN WHERE SOTK=@STKGUI) )
		BEGIN
			DECLARE @SODU_CHUYEN MONEY
			SELECT @SODU_CHUYEN = SODU FROM dbo.TAIKHOAN WHERE @STKGUI = SOTK
  
			IF @SODU_CHUYEN < @TIEN
				RAISERROR('SO DU KHONG DU !!!', 16, 1)
			ELSE
				BEGIN
					BEGIN TRANSACTION
					BEGIN TRY
						UPDATE dbo.TAIKHOAN
						SET SODU -= @TIEN
						WHERE @STKGUI = SOTK

						UPDATE dbo.TAIKHOAN
						SET SODU += @TIEN
						WHERE @STKNHAN = SOTK

						INSERT INTO GD_CHUYENTIEN(SOTK_CHUYEN,NGAYGD,SOTIEN,SOTK_NHAN,MANV) 
							VALUES (@STKGUI,@NGAYGD,@TIEN,@STKNHAN,@MANV)

						COMMIT
					END TRY
					
					BEGIN CATCH		-- một số trường hợp bất ngờ có thể xảy ra gây ra lỗi
						ROLLBACK	-- ví dụ như mất điện khi đang cộng trừ tiền trong tài khoản.
						DECLARE @ERRORMESSAGE VARCHAR(2000)
						SELECT @ERRORMESSAGE = 'Lỗi: ' + ERROR_MESSAGE()
						RAISERROR(@ERRORMESSAGE, 16, 1)
					END CATCH
				END
		END
	ELSE
		RAISERROR('SO TAI KHOAN KHONG TON TAI !!!', 16, 1)
END
